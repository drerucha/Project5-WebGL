<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform sampler2D u_noise;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    float HORIZONTAL_STEP = 1.0 / 1024.0;
    float VERTICAL_STEP = 1.0 / 512.0;

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
    float water(float t, vec2 tc);
    float snoise(vec2 v);

    void main(void)
    {
        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        vec3 spec_map = texture2D( u_EarthSpec, v_Texcoord ).rgb;
        vec3 bump_map = texture2D( u_Bump, v_Texcoord ).rgb;

        // Bump mapping.
        float bump_center, bump_right, bump_top;

        // Land.
        if ( spec_map.r == 0.0 ) {
            bump_center = texture2D( u_Bump, v_Texcoord ).r;
            bump_right = texture2D( u_Bump, vec2( v_Texcoord.s + HORIZONTAL_STEP, v_Texcoord.t ) ).r;
            bump_top = texture2D( u_Bump, vec2( v_Texcoord.s, v_Texcoord.t + VERTICAL_STEP ) ).r;
        }
        // Water.
        else {
            vec2 base_pos = vec2( v_Texcoord.s - u_time / 40.0, v_Texcoord.t - u_time / 60.0 );
            bump_center = texture2D( u_noise, base_pos ).r * 4.0;
            bump_right = texture2D( u_noise, vec2( base_pos.s + HORIZONTAL_STEP, base_pos.t ) ).r * 4.0;
            bump_top = texture2D( u_noise, vec2( base_pos.s, base_pos.t + VERTICAL_STEP ) ).r * 4.0;
        }

        vec3 perturbed_normal = normalize( vec3( bump_center - bump_right, bump_center - bump_top, 0.2 ) );
        mat3 normal_transform = eastNorthUpToEyeCoordinates( v_positionMC, v_Normal );
        vec3 bump_normal = normalize( normal_transform * perturbed_normal );

        float diffuse = clamp( dot( u_CameraSpaceDirLight, bump_normal ), 0.0, 1.0 );

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);

        float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2

        vec2 cloud_texture_coordinates = vec2( v_Texcoord.s - ( u_time / 10.0 ), v_Texcoord.t );
        vec3 cloud_color = texture2D( u_Cloud, cloud_texture_coordinates ).rgb;
        vec3 cloud_map = texture2D( u_CloudTrans, cloud_texture_coordinates ).rgb;

        // Apply gamma correction to nighttime texture.
        nightColor = pow(nightColor,vec3(gammaCorrect));

        // spec_map.r will be 0 if no specularity should be applied, and 1 otherwise.
        dayColor = ((0.6 * diffuse) + (0.4 * specular * spec_map.r )) * dayColor;

        // Update colors based on cloud coverage.
        dayColor = mix( cloud_color, dayColor, cloud_map.r );
        nightColor = mix( vec3( 0.0 ), nightColor, cloud_map.r );

        // Blend day and night textures.
        vec3 color = mix( nightColor, dayColor, clamp( ( diffuse - ( diffuse / 2.0 ) ) * 4.0, 0.0, 1.0 ) );

        // Rim lighting.
        float rim_factor = dot( v_Normal, v_Position ) + 1.0;
        if ( rim_factor > 0.0 ) {
            color += vec3( rim_factor / 4.0, rim_factor / 2.0, rim_factor / 2.0 );
        }

        gl_FragColor = vec4( color, 1.0 );
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }

</script>

<script src ="js/lib/gl-matrix.js" type ="text/javascript"></script>
<script src ="js/webGLUtility.js" type ="text/javascript"></script>
<script src ="js/frag_globe.js" type ="text/javascript"></script>
</body>

</html>
